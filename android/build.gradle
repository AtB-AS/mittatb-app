// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 35
        // Setting androidXBrowser to avoid react-native-reborn picking newest alpha requiring SDK 33
        androidXBrowser = "1.7.0"
        RNMapboxMapsImpl = "mapbox"

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "27.1.12297006"
        kotlin_version = "2.0.21"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
        classpath 'com.google.gms:google-services:4.4.1'
        classpath "com.bugsnag:bugsnag-android-gradle-plugin:8.1.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        def netRcFile = new File('../.netrc').exists() ? new File('../.netrc') : new File('.netrc')

        // For more details, see the requirements setting for Kettle at https://developer.kogenta.com/docs/react-native/add-kettle#declare-repository-manually
        maven { url "https://artifacts.kogenta.com/release" }

        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = 'mapbox'
                // Using same Mapbox token in .netrc file which is used for iOS Mapbox downloads
                password = (netRcFile.text =~ /password (.+)/)[0][1]
            }
        }

        maven {
            url "$entur_artifactory_partner_resolve_snapshot_url"
            credentials {
                username "$entur_artifactory_user"
                password "$entur_artifactory_password"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
        maven {
            url "$entur_artifactory_partner_resolve_release_url"
            credentials {
                username "$entur_artifactory_user"
                password "$entur_artifactory_password"
            }
            mavenContent {
                releasesOnly()
            }
        }
        google()
    }
}

apply plugin: "com.facebook.react.rootproject"