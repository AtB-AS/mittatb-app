// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "30.0.2"
        minSdkVersion = 23
        compileSdkVersion = 31
        targetSdkVersion = 31
        ndkVersion = "21.4.7075529"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:4.2.2')
        classpath 'com.google.gms:google-services:4.2.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        google()
        maven { url 'https://www.jitpack.io' }

        def netRcFile = new File('../.netrc').exists() ? new File('../.netrc') : new File('.netrc')
        def envFile = new File('../.env').exists() ? new File('../.env') : new File('.env')

        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = 'mapbox'
                // Using same Mapbox token in .netrc file which is used for iOS Mapbox downloads
                password = (netRcFile.text =~ /password (.+)/)[0][1]
            }
        }

        maven {
            url "https://entur2.jfrog.io/entur2/partner-snapshot"
            credentials {
                username = (envFile.text =~ /ENTUR_REGISTRY_USER=(.+)/)[0][1]
                password = (envFile.text =~ /ENTUR_REGISTRY_PASSWORD=(.+)/)[0][1]
            }
            mavenContent {
                snapshotsOnly()
            }
        }
        maven {
            url "https://entur2.jfrog.io/entur2/partner-release"
            credentials {
                username = (envFile.text =~ /ENTUR_REGISTRY_USER=(.+)/)[0][1]
                password = (envFile.text =~ /ENTUR_REGISTRY_PASSWORD=(.+)/)[0][1]
            }
            mavenContent {
                releasesOnly()
            }
        }
    }
}
